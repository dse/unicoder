#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;

use File::Basename qw(basename);
our $PROGNAME = basename($0);

use open IO => ':locale';
use Encode qw(decode);
use Encode::Locale;

@ARGV = map { decode('locale', $_, 1) } @ARGV;

use Getopt::Long;

use FindBin;
use lib "${FindBin::Bin}/../lib";

use Unicoder;

our $dry_run = 0;
our $verbose = 0;

sub usage { print <<END; }
usage:
    $PROGNAME [<option> ...] <command> [<arguments> ...]
commands:
    $PROGNAME list-blocks
    $PROGNAME list-scripts
    $PROGNAME script '<scriptname>'
    $PROGNAME block '<blockname>'
    $PROGNAME table '<blockname>'
    $PROGNAME charinfo '<char>'
    $PROGNAME charprops '<char>'     single char., codepoint, or char. name
    $PROGNAME search <keyword> ...   can use -<keyword> or +<keyword>
    $PROGNAME help
options:
    --dumper
    --tsv, --tab-separated-values
    --csv, --comma-separated-values
    --text
    --dec, --decimal
    --oct, --octal
    --hex, --hexadecimal
END

our $format;
our $base;

my $p = Getopt::Long::Parser->new();
$p->configure('gnu_compat', 'bundling', 'no_ignore_case');
$p->getoptionsfromarray(
    \@ARGV,
    'h|help'                     => sub { usage; exit 0; },
    'tsv|tab-separated-values'   => sub { $format = 'tsv'; },
    'csv|comma-separated-values' => sub { $format = 'csv'; },
    'text'                       => sub { $format = 'text'; },
    'hex|hexadecimal'            => sub { $base = 16; },
    'oct|octal'                  => sub { $base = 8; },
    'dec|decimal'                => sub { $base = 10; },
    'dumper|data-dumper'         => sub { $format = 'dumper'; },
    'v|verbose+'                 => \$verbose,
    'n|dry-run'                  => \$dry_run,
) or die("Type '$PROGNAME --help' for help.\n");

my ($command, @arguments) = @ARGV;
if (!defined $command) {
    die("No command specified.\n");
}

my $unicoder = Unicoder->new();
$unicoder->format($format) if defined $format;
$unicoder->base($base)     if defined $base;

if ($command =~ m{^(list-?)?blocks$}i) {
    $unicoder->listBlocks();
} elsif ($command =~ m{^(list-?)?scripts$}i) {
    $unicoder->listScripts();
} elsif ($command =~ m{^char(acter)?(-?info)?$}i) {
    foreach my $argument (@arguments) {
        $unicoder->charInfo($argument);
    }
} elsif ($command =~ m{^char(acter)?(-?prop(?:s|erties))?$}i) {
    foreach my $argument (@arguments) {
        $unicoder->charProperties($argument);
    }
} elsif ($command =~ m{^search$}i) {
    $unicoder->search(@arguments);
} elsif ($command =~ m{^help$}i) {
    usage();
    exit(0);
} elsif ($command =~ m{^block$}i) {
    $unicoder->listBlock(@arguments);
} elsif ($command =~ m{^table$}i) {
    $unicoder->listBlockTable(@arguments);
} elsif ($command =~ m{^build$}i) {
    $unicoder->buildDatabase();
    $unicoder->buildTextDatabase();
    $unicoder->buildTextList();
} elsif ($command =~ m{^listall$}i) {
    $unicoder->listAll();
} else {
    die("No such command: $command\n");
}
